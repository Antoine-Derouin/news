plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "androidx.navigation.safeargs.kotlin"
    id "com.squareup.sqldelight"
}

def signingPropertiesFile = rootProject.file("signing.properties")

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "co.appreactor.news"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 15
        versionName "0.2.9"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "news-$versionName")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
    }

    signingConfigs {
        if (signingPropertiesFile.exists()) {
            play {
                def signingProperties = new Properties()
                signingProperties.load(new FileInputStream(signingPropertiesFile))
                storeFile file(signingProperties['playKeystoreFile'])
                storePassword signingProperties['playKeystorePassword']
                keyAlias signingProperties['playKeyAlias']
                keyPassword signingProperties['playKeyPassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
        }

        if (signingPropertiesFile.exists()) {
            play {
                def signingProperties = new Properties()
                signingProperties.load(new FileInputStream(signingPropertiesFile))
                signingConfig signingConfigs.play
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        disable "VectorRaster", "VectorPath", "InvalidFragmentVersionForActivityResult", "MissingTranslation"
    }
}

sqldelight {
    Database {
        packageName = "co.appreactor.news"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions_coroutines"

    implementation "co.appreactor:feedk:$versions_feedk"

    implementation "androidx.core:core-ktx:$versions_ktx"
    implementation "androidx.fragment:fragment-ktx:$versions_fragment"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions_lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions_lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions_lifecycle"
    implementation "androidx.navigation:navigation-ui-ktx:$versions_nav"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions_nav"
    implementation "androidx.constraintlayout:constraintlayout:$versions_constrained_layout"
    implementation "androidx.recyclerview:recyclerview:$versions_recyclerview"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions_swipe_refresh"
    implementation "androidx.work:work-runtime-ktx:$versions_work"

    implementation "com.squareup.retrofit2:retrofit:$versions_retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions_retrofit"
    implementation "com.squareup.okhttp3:okhttp-bom:$versions_okhttp"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    implementation "com.github.nextcloud:Android-SingleSignOn:$versions_nextcloud_sso"
    implementation "com.google.android.material:material:$versions_material"
    implementation "com.squareup.picasso:picasso:$versions_picasso"
    implementation "io.insert-koin:koin-android:$versions_koin"
    implementation "io.insert-koin:koin-android-ext:$versions_koin"
    implementation "io.insert-koin:koin-android-viewmodel:$versions_koin"
    implementation "com.squareup.sqldelight:android-driver:$versions_sqldelight"
    implementation "com.squareup.sqldelight:coroutines-extensions:$versions_sqldelight"
    implementation "com.jakewharton.timber:timber:$versions_timber"
    implementation "org.jsoup:jsoup:$versions_jsoup"
    implementation "joda-time:joda-time:$versions_joda_time"

    testImplementation "junit:junit:$versions_junit"
    testImplementation "io.mockk:mockk:$versions_mockk"
    testImplementation "com.squareup.sqldelight:sqlite-driver:$versions_sqldelight"

    androidTestImplementation "androidx.test:runner:$versions_test_runner"
    androidTestImplementation "androidx.test:rules:$versions_test_rules"
}
